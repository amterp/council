#!/usr/bin/env rad
---
Local development script for council.
---

args:
    build b bool                      # Build the project
    validate v bool                   # Build and run tests
    push p bool                       # Push up to origin
    release r str?                    # Release type (patch, minor, major)
    tap_path str = "../homebrew-tap"  # Path to homebrew-tap repo

    release enum ["patch", "minor", "major"]

if build or validate or push or release:
    $`go build -o council ./cmd/council`

if validate or push or release:
    $`go test ./...`

if push:
    code, stdout = quiet $`git status --porcelain -uno`
    if stdout != "":
        print_err("Git status is not clean, aborting push")
        exit(1)
    stdout = quiet $`git branch --show-current`
    branch = stdout.trim()
    $`git push origin {branch}`

if release:
    // Ensure we're on main branch
    stdout = quiet $`git branch --show-current`
    branch = stdout.trim()
    if branch != "main":
        print_err("Release must be run from main branch (currently on '{branch}')")
        exit(1)

    // Calculate new version from latest tag
    stdout = quiet $`git tag -l 'v*' --sort=-v:refname`
    tags = stdout.trim().split("\n")
    if tags.len() == 0 or tags[0] == "":
        print_err("No existing tags found. Create initial tag v0.0.0 first.")
        exit(1)
    latest_tag = tags[0]

    // Parse version (strip leading 'v')
    version_str = latest_tag[1:]
    parts = version_str.split("\\.")
    major = parse_int(parts[0])
    minor = parse_int(parts[1])
    patch = parse_int(parts[2])

    print("Current version: {version_str}")

    // Bump based on type
    switch release:
        case "patch":
            patch += 1
        case "minor":
            minor += 1
            patch = 0
        case "major":
            major += 1
            minor = 0
            patch = 0

    release = "{major}.{minor}.{patch}"
    print("New version: {release}")

    // Paths
    stdout = quiet $`pwd`
    repo = stdout.trim()
    releases_dir = "{repo}/releases"
    formula_path = "{repo}/{tap_path}/Formula/council.rb"
    tag = "v{release}"

    // Build for darwin-arm64
    print("Building darwin-arm64...")
    $`GOOS=darwin GOARCH=arm64 go build -o council ./cmd/council`
    $`mkdir -p {releases_dir}`
    arm64_tar = "{releases_dir}/council-{release}-darwin-arm64.tar.gz"
    quiet $`rm -f {arm64_tar}` catch:
        pass
    $`tar -czf {arm64_tar} council`

    // Build for darwin-amd64
    print("Building darwin-amd64...")
    $`GOOS=darwin GOARCH=amd64 go build -o council ./cmd/council`
    amd64_tar = "{releases_dir}/council-{release}-darwin-amd64.tar.gz"
    quiet $`rm -f {amd64_tar}` catch:
        pass
    $`tar -czf {amd64_tar} council`

    // Clean up binary
    quiet $`rm -f council` catch:
        pass

    // Calculate SHA256
    stdout = quiet $`shasum -a 256 {arm64_tar}`
    sha256_arm64 = stdout.split(" ")[0]
    stdout = quiet $`shasum -a 256 {amd64_tar}`
    sha256_amd64 = stdout.split(" ")[0]

    // Git tag
    print("Tagging {tag}...")
    $`git tag {tag}`

    // GitHub release
    print("Creating GitHub release...")
    $`gh release create {tag} {arm64_tar} {amd64_tar} --repo amterp/council --title "Council {tag}" --notes "Council {tag}"`

    // Update formula
    print("Updating formula...")
    formula = """
class Council < Formula
  desc "CLI tool for collaborative multi-agent sessions"
  homepage "https://github.com/amterp/council"
  version "{release}"
  license "Apache-2.0"

  on_macos do
    if Hardware::CPU.arm?
      url "https://github.com/amterp/council/releases/download/v#\{version}/council-#\{version}-darwin-arm64.tar.gz"
      sha256 "{sha256_arm64}"
    else
      url "https://github.com/amterp/council/releases/download/v#\{version}/council-#\{version}-darwin-amd64.tar.gz"
      sha256 "{sha256_amd64}"
    end
  end

  def install
    bin.install "council"
  end

  test do
    system "#\{bin}/council", "--help"
  end
end
"""
    write_file(formula_path, formula)

    // Push tap
    print("Pushing tap...")
    tap = "{repo}/{tap_path}"
    $`git -C {tap} add Formula/council.rb`
    $`git -C {tap} commit -m "council {release}"`
    $`git -C {tap} push`

    // Push council repo (tag already pushed by gh release)
    print("Pushing council...")
    $`git push origin main`

    print("Done! {tag} released.")
